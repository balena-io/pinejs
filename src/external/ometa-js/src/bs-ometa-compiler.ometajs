export ometa BSOMetaParser {
  space          = ^space | fromTo('//', '\n') | fromTo('/*', '*/'),
  nameFirst      = '_' | '$' | letter,
  nameRest       = nameFirst | digit,
  tsName         = <nameFirst nameRest*>,
  name           = spaces tsName,
  eChar          = escapedChar
                 | char,
  tsString       = '\'' (~'\'' eChar)*:xs '\''                         -> xs.join(''),
  seqString      = '`' '`' (~('\'' '\'') eChar)*:xs '\'' '\''          -> [#App, #seq,     JSON.stringify(xs.join(''))],
  tokenString    = '"'     (~'"'         eChar)*:xs '"'                -> [#App, #token,   JSON.stringify(xs.join(''))],
  string         = (('#' | '`') tsName | tsString):xs                  -> [#App, #exactly, JSON.stringify(xs)],
  number         = <'-'? digit+>:n                                     -> [#App, #exactly, n],
  keyword :xs    = token(xs) ~letterOrDigit                            -> xs,
  args           = '(' listOf(#hostExpr, ','):xs ")"                   -> xs
                 | empty                                               -> [],
  application    = "^"          name:rule args:as                      -> [#App, "super",        "'" + rule + "'"].concat(as)
                 | name:grm "." name:rule args:as                      -> [#App, "foreign", grm, "'" + rule + "'"].concat(as)
                 |              name:rule args:as                      -> [#App, rule].concat(as),
  hostExpr       = BSSemActionParser.asgnExpr:r                           BSJSTranslator.trans(r),
  curlyHostExpr  = BSSemActionParser.curlySemAction:r                     BSJSTranslator.trans(r),
  primHostExpr   = BSSemActionParser.semAction:r                          BSJSTranslator.trans(r),
  atomicHostExpr = curlyHostExpr | primHostExpr,
  semAction      = curlyHostExpr:x                                     -> [#Act, x]
                 | "!"  atomicHostExpr:x                               -> [#Act, x],
  arrSemAction   = "->" atomicHostExpr:x                               -> [#Act, x],
  semPred        = "?"  atomicHostExpr:x                               -> [#Pred, x],
  expr           = expr5(true):x ("|"  expr5(true))+:xs                -> [#Or,  x].concat(xs)
                 | expr5(true):x ("||" expr5(true))+:xs                -> [#XOr, x].concat(xs)
                 | expr5(false),
  expr5 :ne      = interleavePart:x ("&&" interleavePart)+:xs          -> [#Interleave, x].concat(xs)
                 | expr4(ne),
  interleavePart = "(" expr4(true):part ")"                            -> ["1", part]
                 | expr4(true):part modedIPart(part),
  modedIPart     = [#And [#Many  :part]]                               -> ["*", part]
                 | [#And [#Many1 :part]]                               -> ["+", part]
                 | [#And [#Opt   :part]]                               -> ["?", part]
                 | :part                                               -> ["1", part],
  expr4 :ne      =                expr3*:xs arrSemAction:act           -> [#And].concat(xs).concat([act])
                 | ?ne            expr3+:xs                            -> [#And].concat(xs)
                 | ?(ne == false) expr3*:xs                            -> [#And].concat(xs),
  optIter :x     = '*'                                                 -> [#Many,  x]
                 | '+'                                                 -> [#Many1, x]
                 | '?'                                                 -> [#Opt,   x]
                 | empty                                               -> x,
  optBind :x     = ':' name:n                                          -> { this.locals[n] = true; [#Set, n, x] }
                 | empty                                               -> x,
  expr3          = ":" name:n                                          -> { this.locals[n] = true; [#Set, n, [#App, #anything]] }
                 | (expr2:x optIter(x) | semAction):e optBind(e)
                 | semPred,
  expr2          = "~" expr2:x                                         -> [#Not,       x]
                 | "&" expr1:x                                         -> [#Lookahead, x]
                 | expr1,
  expr1          = application 
                 | ( keyword('undefined') | keyword('nil')
                   | keyword('true')      | keyword('false') ):x       -> [#App, #exactly, x]
                 | spaces (seqString | tokenString | string | number)
                 | "["  expr:x "]"                                     -> [#Form,      x]
                 | "<"  expr:x ">"                                     -> [#ConsBy,    x]
                 | "@<" expr:x ">"                                     -> [#IdxConsBy, x]
                 | "("  expr:x ")"                                     -> x,
  param          = ":" name:n                                          -> n,
  ruleName       = name
                 | spaces tsString,
  rule           = &(ruleName:n) !(this.locals = {'$elf=this': true, '_fromIdx=this.input.idx': true})
                     !(this.params = [])
                     rulePart(n):x ("," rulePart(n))*:xs               -> [#Rule, n, this.params, Object.getOwnPropertyNames(this.locals).sort(), [#Or, x].concat(xs)],
  rulePart :rn   = ruleName:n ?(n == rn)
                   ( param*:p "=" {this.params = this.params.concat(p)} expr:b
                   | expr:b)                                           -> b,
  grammar        = (keyword('export') -> true | -> false):exported keyword('ometa') name:n
                     ( "<:" name | empty -> 'OMeta' ):sn
                     "{" listOf(#rule, ','):rs "}"                     -> [#Grammar, exported, n, sn].concat(rs)
}
BSOMetaParser._enableTokens = function() {
  OMeta._enableTokens.call(this, ['keyword', 'ruleName', 'seqString', 'tokenString', 'string']);
};

// By dispatching on the head of a list, the following idiom allows translators to avoid doing a linear search.
// (Note that the "=" in a rule definition is optional, so you can give your rules an "ML feel".)
export ometa BSOMetaTranslator {
  App        'super' anything+:args        -> [this.sName, '._superApplyWithArgs(this,', args.join(','), ')']      .join(''),
  App        :rule   anything+:args        -> ['this._applyWithArgs("', rule, '",',      args.join(','), ')']      .join(''),
  App        :rule                         -> ['this._apply("', rule, '")']                                        .join(''),
  Act        :expr                         -> expr,
  Pred       :expr                         -> ['this._pred(', expr, ')']                                           .join(''),
  Or         transFn*:xs                   -> ['this._or(',  xs.join(','), ')']                                    .join(''),
  XOr        transFn*:xs                       { JSON.stringify(xs.unshift((this.name + "." + this.rName))) }
                                           -> ['this._xor(', xs.join(','), ')']                                    .join(''),
  Seq        notLast(#trans)*:xs trans:y
             {xs.push('return ' + y)}      -> xs.join(';'),
  Seq                                      -> 'undefined',
  And        Seq:xs                        -> ['(function(){', xs, '}).call(this)']                                .join(''),
  Opt        transFn:x                     -> ['this._opt(',           x, ')']                                     .join(''),
  Many       transFn:x                     -> ['this._many(',          x, ')']                                     .join(''),
  Many1      transFn:x                     -> ['this._many1(',         x, ')']                                     .join(''),
  Set        :n trans:v                    -> [n, '=', v]                                                          .join(''),
  Not        transFn:x                     -> ['this._not(',           x, ')']                                     .join(''),
  Lookahead  transFn:x                     -> ['this._lookahead(',     x, ')']                                     .join(''),
  Form       transFn:x                     -> ['this._form(',          x, ')']                                     .join(''),
  ConsBy     transFn:x                     -> ['this._consumedBy(',    x, ')']                                     .join(''),
  IdxConsBy  transFn:x                     -> ['this._idxConsumedBy(', x, ')']                                     .join(''),
  JumpTable  jtCase*:cases                 -> this.jumpTableCode(cases),
  Interleave intPart*:xs                   -> ['this._interleave(', xs.join(','), ')']                             .join(''),
  
  Rule       :name {this.rName = name}
             params:ps locals:ls
             ( ['And' Seq:body]
             | trans:body {['return ', body].join('')}:body
             )                             -> ['\n"', name, '":function(', ps, '){', ls, body, '}']             .join(''),
  Grammar    :exported :name :sName
             {this.name = name}
             {this.sName = sName}
             trans*:rules                  -> ['var ', name, (exported ? "=exports." + name : ''), '=', sName, '._extend({', rules.join(','), '})'].join(''),
  intPart  = [:mode transFn:part]          -> (JSON.stringify(mode)  + "," + part),
  jtCase   = [:x trans:e]                  -> [JSON.stringify(x), e],
  locals   = [string+:vs]                  -> ['var ', vs.join(','), ';']                                          .join('')
           | []                            -> '',
  params   = [string+:vs]                  -> vs.join(',')
           | []                            -> '',
  trans    = [:t apply(t):ans]             -> ans,
  transFn  = ( ['And' Seq:x]
             | trans:x {['return ', x].join('')}:x
             )                             -> ['(function(){', x, '})']                                            .join('')
}
BSOMetaTranslator.jumpTableCode = function(cases) {
  var buf = [];
  buf.push("(function(){switch(this._apply('anything')){");
  for (var i = 0; i < cases.length; ++i) {
    buf.push("case " + cases[i][0] + ":return " + cases[i][1] + ";");
  }
  buf.push("default: throw this._fail()}}).call(this)");
  return buf.join('');
}

